arch="atmega328p"

VariantDir('build','src')

####################################################################################################
# common code
env=Environment(
    AS="avr-as",
    CC='avr-gcc',
    CXX="avr-gcc",
    CCFLAGS="-D F_CPU=12000000L -mmcu=%s -std=c++11 -Wall -Os -funroll-loops -I./lib -I../ -I../nanopb -I../bazel-genfiles -I%s -fdata-sections -ffunction-sections"%(arch, "/usr/share/arduino/hardware/arduino/avr/variants/standard"),
    LINKFLAGS="-mmcu=%s -Wl,--gc-sections"%arch)

env.Append(BUILDERS=
    {"Copy":Builder(action="avr-objcopy $SOURCE -O ihex $TARGET")
    ,"Dump":Builder(action="avr-objdump -dSr $SOURCE > $TARGET")
    # ATmega328PB sig=0x1e9516  /  ATmega328P is 1E 95 0F
    # Ignore sig because avrdude doesn't support it yet.
    ,"WriteProg":Builder(action="avrdude -F -p m328p -c usbasp -U flash:w:$SOURCE:i")
    ,"CheckSize":Builder(action="avr-size $SOURCE")
    })

# http://www.engbedded.com/fusecalc/
# 16MHz 1:1, fast start, no watchdog, preserve EEPROM while flashing
env.Alias('writefuse',env.Command('fake-fuse', None, "avrdude -F -p m328p -c usbasp -U lfuse:w:0xfe:m -U hfuse:w:0xd1:m"))

env.Library('build/libs.a', Glob('lib/*.cpp') + Glob('lib/*.c'))

####################################################################################################
# different FWs

# TODO: Somehow, when bazel is invoked from within scons, it messes up output dir by failing to create symlinks.
# For now, invoke "bazel build //proto:proto_nanopb" manually before calling "scons", whenever .proto files are updated.
# bazel build //proto:proto_nanopb && scons

code_objs  = []
for x in Glob('src/*.cpp'):
    fn = x.name[:-4]
    obj = 'build/builder-%s.o' % fn
    code_objs.append(obj)
    env.Object(obj, 'build/%s.cpp' % fn)
env.Object('build/proto.o', '../bazel-genfiles/proto/builder.pb.c')
env.Object('build/pb_encode.o', '../nanopb/pb_encode.c')
env.Object('build/pb_common.o', '../nanopb/pb_common.c')

env.Program('build/builder-fw.elf', code_objs + ['build/libs.a', 'build/proto.o', 'build/pb_encode.o', 'build/pb_common.o'])
env.Copy('build/builder-fw.hex', 'build/builder-fw.elf')
env.Dump('build/builder-disasm.S', 'build/builder-fw.elf')
env.Alias('write-builder', env.WriteProg(None, 'build/builder-fw.hex'))
env.Alias('size-builder', env.CheckSize('fake-sz-builder', 'build/builder-fw.elf'))

env.Default('size-builder')
